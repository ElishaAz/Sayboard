apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

repositories {
    google()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.elishaazaria.sayboard"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 3
        versionName "v2.0.1"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64', 'x86'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'com.elishaazaria.sayboard'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.7"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.52.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { option 'lite' }
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'net.java.dev.jna:jna:5.9.0@aar'
    implementation group: 'com.alphacephei', name: 'vosk-android', version: '0.3.32'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.navigation:navigation-fragment:2.5.2'
    implementation 'androidx.navigation:navigation-ui:2.5.2'
    implementation 'androidx.preference:preference:1.2.0'

    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'com.github.martin-stone:hsv-alpha-color-picker-android:3.0.1'
    implementation 'dev.gustavoavila:java-android-websocket-client:2.0.1'

    implementation 'io.grpc:grpc-okhttp:1.52.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.52.0'
    implementation 'io.grpc:grpc-stub:1.52.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}
